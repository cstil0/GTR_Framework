//example of some shaders compiled
flat basic.vs flat.fs
texture basic.vs texture.fs
multi_pass basic.vs light_multi_pass.fs
single_pass basic.vs light_single_pass.fs
depth quad.vs depth.fs
multi basic.vs multi.fs
//------------------------------------------------------------------
\basic.vs

#version 330 core

in vec3 a_vertex;
in vec3 a_normal;
in vec2 a_coord;
in vec4 a_color;

uniform vec3 u_camera_pos;

uniform mat4 u_model;
uniform mat4 u_viewprojection;

//this will store the color for the pixel shader
out vec3 v_position;
out vec3 v_world_position;
out vec3 v_normal;
out vec2 v_uv;
out vec4 v_color;

uniform float u_time;

void main()
{	
	//calcule the normal in camera space (the NormalMatrix is like ViewMatrix but without traslation)
	v_normal = (u_model * vec4( a_normal, 0.0) ).xyz;
	
	//calcule the vertex in object space
	v_position = a_vertex;
	v_world_position = (u_model * vec4( v_position, 1.0) ).xyz;
	
	//store the color in the varying var to use it from the pixel shader
	v_color = a_color;

	//store the texture coordinates
	v_uv = a_coord;

	//calcule the position of the vertex using the matrices
	gl_Position = u_viewprojection * vec4( v_world_position, 1.0 );
}
//------------------------------------------------------------------
\quad.vs

#version 330 core

in vec3 a_vertex;
in vec2 a_coord;
out vec2 v_uv;

void main()
{	
	v_uv = a_coord;
	gl_Position = vec4( a_vertex, 1.0 );
}

//-------------------------------------------------------------
\flat.fs

#version 330 core

uniform vec4 u_color;

out vec4 FragColor;

void main()
{
	FragColor = u_color;
}

//------------------------------------------------------------
\texture.fs

#version 330 core

in vec3 v_position;
in vec3 v_world_position;
in vec3 v_normal;
in vec2 v_uv;
in vec4 v_color;

uniform vec4 u_color;
uniform sampler2D u_texture;
uniform float u_time;
uniform float u_alpha_cutoff;

out vec4 FragColor;

void main()
{
	vec2 uv = v_uv;
	vec4 color = u_color;
	color *= texture( u_texture, v_uv );

	if(color.a < u_alpha_cutoff)
		discard;

	FragColor = color;
}

//------------------------------------------------------------------------------------------------------------------------------
\light_single_pass.fs
#version 330 core

in vec3 v_position;
in vec3 v_world_position;
in vec3 v_normal;
in vec2 v_uv;
in vec4 v_color;

uniform vec4 u_color;
uniform sampler2D u_texture;
uniform float u_time;
uniform float u_alpha_cutoff;

// Light parameters
uniform vec3 u_ambient_light;

// -- Single Pass
#define MAX_LIGHTS 10
uniform int u_lights_type[MAX_LIGHTS];
uniform vec3 u_lights_position[MAX_LIGHTS];
uniform vec3 u_lights_color[MAX_LIGHTS];
uniform float u_lights_max_distance[MAX_LIGHTS];
uniform vec3 u_lights_direction[MAX_LIGHTS];
uniform float u_lights_cone_cos[MAX_LIGHTS];
uniform float u_lights_cone_exp[MAX_LIGHTS];

out vec4 FragColor;
// Save computations related to light
struct LightStruct{
	vec3 L;
	vec3 D;

	float NdotL;
	float LdotD;
	float light_dist;

	float spot_factor;
	float att_factor;
}LightComp;

void computeL_point(inout LightStruct lc, int i){
	// Distance from current point to light
	vec3 L = u_lights_position[i] - v_world_position;
	// Modulus			
	lc.light_dist = length(L);
	// Normalize L
	lc.L = L/lc.light_dist;
}

void computeL_directional(inout LightStruct lc, int i){
	vec3 L = normalize(u_lights_position[i]);
	lc.light_dist = length(L);
	lc.L = L/lc.light_dist;
}

void computeNdotL(inout LightStruct lc){
	vec3 N = normalize(v_normal);
	lc.NdotL = clamp(dot(lc.L,N), 0.0, 1.0);
}

void computeSpotFactor(inout LightStruct lc, int i){
	lc.D = normalize(u_lights_direction[i]);
	lc.LdotD = dot(-lc.L, lc.D);
	if (lc.LdotD >= u_lights_cone_cos[i]){
		// How much point is inside spotlight
		lc.spot_factor = pow(lc.LdotD, u_lights_cone_exp[i]);	
	}
	else{
		lc.spot_factor = 0.0;
	}	
}

void computeAttenuation(inout LightStruct lc, int i){
	float att_factor = u_lights_max_distance[i] - lc.light_dist;
	att_factor /= u_lights_max_distance[i];
	lc.att_factor = max(att_factor, 0.0);
}

void main()
{
	vec2 uv = v_uv;
	vec4 color = u_color;
	color *= texture( u_texture, v_uv );

	if(color.a < u_alpha_cutoff)
		discard;

	vec3 light = vec3(u_ambient_light);

	// Iterate lights
	for (int i = 0; i < MAX_LIGHTS; i++) {
		LightComp = LightStruct(
			vec3(0.0, 0.0, 0.0), //L
			vec3(0.0, 0.0, 0.0), //DdotL

			0.0, // NdotL
			0.0, // LdotD
			0.0, // light dist

			1.0, // spot factor
			0.0 // attenuation factor
		);

		// Point
		if (u_lights_type[i] == 1){
			computeL_point(LightComp, i);
		}

		// Spot
		else if (u_lights_type[i] == 2){
			computeL_point(LightComp, i);
			computeSpotFactor(LightComp, i);
		}

		// Directional
		else if (u_lights_type[i] == 3){
			computeL_directional(LightComp, i);
		}

		computeNdotL(LightComp);
		computeAttenuation(LightComp, i);
		light += u_lights_color[i] * LightComp.NdotL * pow(LightComp.att_factor,2) * LightComp.spot_factor;
	}
	color.xyz *= light;
	FragColor = color;
}

//----------------------------------------------------------------------------------------------------------------------------
\light_multi_pass.fs
#version 330 core

in vec3 v_position;
in vec3 v_world_position;
in vec3 v_normal;
in vec2 v_uv;
in vec4 v_color;

uniform vec4 u_color;
uniform sampler2D u_texture;
uniform float u_time;
uniform float u_alpha_cutoff;

// Light parameters
uniform vec3 u_ambient_light;

uniform int u_light_type;
uniform vec3 u_light_position;
uniform vec3 u_light_color;
uniform float u_light_max_distance;
uniform vec3 u_light_direction;
uniform float u_light_cone_cos;
uniform float u_light_cone_exp;

out vec4 FragColor;

// Save computations related to light
struct LightStruct{
	vec3 L;
	vec3 D;

	float NdotL;
	float LdotD;
	float light_dist;

	float spot_factor;
	float att_factor;
}LightComp;

void computeL_point(inout LightStruct lc){
	// Distance from current point to light
	vec3 L = u_light_position - v_world_position;
	// Modulus			
	lc.light_dist = length(L);
	// Normalize L
	lc.L = L/lc.light_dist;
}

void computeL_directional(inout LightStruct lc){
	vec3 L = normalize(u_light_position);
	lc.light_dist = length(L);
	lc.L = L/lc.light_dist;
}

void computeNdotL(inout LightStruct lc){
	vec3 N = normalize(v_normal);
	lc.NdotL = clamp(dot(lc.L,N), 0.0, 1.0);
}

void computeSpotFactor(inout LightStruct lc){
	lc.D = normalize(u_light_direction);
	lc.LdotD = dot(-lc.L, lc.D);
	if (lc.LdotD >= u_light_cone_cos){
		// How much point is inside spotlight
		lc.spot_factor = pow(lc.LdotD, u_light_cone_exp);	
	}
	else{
		lc.spot_factor = 0.0;
	}	
}

void computeAttenuation(inout LightStruct lc){
	float att_factor = u_light_max_distance - lc.light_dist;
	att_factor /= u_light_max_distance;
	lc.att_factor = max(att_factor, 0.0);
}

void main()
{
	LightComp = LightStruct(
		vec3(0.0, 0.0, 0.0), //L
		vec3(0.0, 0.0, 0.0), //DdotL

		0.0, // NdotL
		0.0, // LdotD
		0.0, // light dist

		1.0, // spot factor
		0.0 // attenuation factor
	);

	vec2 uv = v_uv;
	vec4 color = u_color;
	color *= texture( u_texture, v_uv );

	if(color.a < u_alpha_cutoff)
		discard;

	vec3 light = vec3(u_ambient_light);

	// Point
	if (u_light_type == 1){
		computeL_point(LightComp);
	}

	// Spot
	else if (u_light_type == 2){
		computeL_point(LightComp);
		computeSpotFactor(LightComp);
	}

	// Directional
	else if (u_light_type == 3){
		computeL_directional(LightComp);
	}

	computeNdotL(LightComp);
	computeAttenuation(LightComp);
	light += u_light_color * LightComp.NdotL * pow(LightComp.att_factor,2) * LightComp.spot_factor;
	color.xyz *= light;
	FragColor = color;
}
 
//------------------------------------------------------------
\multi.fs

#version 330 core

in vec3 v_position;
in vec3 v_world_position;
in vec3 v_normal;
in vec2 v_uv;

uniform vec4 u_color;
uniform sampler2D u_texture;
uniform float u_time;
uniform float u_alpha_cutoff;

layout(location = 0) out vec4 FragColor;
layout(location = 1) out vec4 NormalColor;

void main()
{
	vec2 uv = v_uv;
	vec4 color = u_color;
	color *= texture( u_texture, uv );

	if(color.a < u_alpha_cutoff)
		discard;

	vec3 N = normalize(v_normal);

	FragColor = color;
	NormalColor = vec4(N,1.0);
}


\depth.fs

#version 330 core

uniform vec2 u_camera_nearfar;
uniform sampler2D u_texture; //depth map
in vec2 v_uv;
out vec4 FragColor;

void main()
{
	float n = u_camera_nearfar.x;
	float f = u_camera_nearfar.y;
	float z = texture2D(u_texture,v_uv).x;
	float color = n * (z + 1.0) / (f + n - z * (f - n));
	FragColor = vec4(color);
}


\instanced.vs

#version 330 core

in vec3 a_vertex;
in vec3 a_normal;
in vec2 a_coord;

in mat4 u_model;

uniform vec3 u_camera_pos;

uniform mat4 u_viewprojection;

//this will store the color for the pixel shader
out vec3 v_position;
out vec3 v_world_position;
out vec3 v_normal;
out vec2 v_uv;

void main()
{	
	//calcule the normal in camera space (the NormalMatrix is like ViewMatrix but without traslation)
	v_normal = (u_model * vec4( a_normal, 0.0) ).xyz;
	
	//calcule the vertex in object space
	v_position = a_vertex;
	v_world_position = (u_model * vec4( a_vertex, 1.0) ).xyz;
	
	//store the texture coordinates
	v_uv = a_coord;

	//calcule the position of the vertex using the matrices
	gl_Position = u_viewprojection * vec4( v_world_position, 1.0 );
}